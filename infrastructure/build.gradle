apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco-android'

// This is the library version used when deploying the artifact
version = "0.0.13"

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName version
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets { main { res.srcDirs = ['src/main/res', 'src/test/res'] } }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile 'com.android.support:support-annotations:25.3.1'
    compile "android.arch.lifecycle:runtime:1.1.0"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.0"
    testCompile "android.arch.lifecycle:runtime:1.1.0"
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.11.0'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

def siteUrl = 'https://github.com/albertopeam/Android-Architecture-Components'      // Homepage URL of the library
def gitUrl = 'https://github.com/albertopeam/Android-Architecture-Components.git'   // Git repository URL
group = "com.github.albertopeam"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                inceptionYear '2017'
                packaging 'aar'

                // Add your description here
                name 'albertopeam.android:infrastructure'
                description = 'General purpose library for make use of multithreading and exception handling in Android.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/albertopeam/Android-Architecture-Components/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'albertopeam'
                        name 'Alberto Penas Amor'
                        email 'albertopeam@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))  + configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

//MANUALLY: :infrastructure -> Tasks-> other: install | bintrayUpload
bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")

    configurations = ['archives']
    pkg {
        repo = "android"
        name = "Concurrency"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
        version {
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = System.getenv("BINTRAY_GPG_PASSWD") //Optional. The passphrase for GPG signing'
            }
        }
    }
}

jacoco {
    toolVersion = "0.7.7.201606060606"
}
